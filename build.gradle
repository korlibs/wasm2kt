buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

group 'com.soywiz'
version '0.0.3'

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.jfrog.bintray'

mainClassName = "com.soywiz.wasm.Wasm"

repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/soywiz/soywiz/" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.soywiz:korio:0.24.0"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version "$project.version"
        }
    }
}

kotlin.experimental.coroutines "enable"

test {
    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }
}

// gradle bintrayUpload
if (hasProperty('BINTRAY_USER') && hasProperty('BINTRAY_KEY')) {
    bintray {
        user = property('BINTRAY_USER')
        key = property('BINTRAY_KEY')
        publications = ['MyPublication']
        publish = true
        override = true

        pkg {
            repo = "soywiz"
            name = "wasm2kt"
            userOrg = 'soywiz'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/soywiz/wasm2kt.git'
        }
    }
}

task deploy(dependsOn: ['install', 'bintrayUpload']) {
}
